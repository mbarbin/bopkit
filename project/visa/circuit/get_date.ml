(* A program to generate initial values for Visa's RAM memory. *)

let cmd =
  Command.make
    ~summary:"generate initial contents for visa's RAM memory"
    (let%map_open.Command ofday =
       Arg.pos ~pos:0 Param.string ~docv:"HH:MM:SS" ~doc:"time of day"
       >>| Core.Time_ns.Ofday.of_string
       >>| Core.Time_ns.Ofday.to_parts
     and date =
       Arg.pos ~pos:1 Param.string ~docv:"YYYY/MM/DD" ~doc:"date" >>| Core.Date.of_string
     in
     let print name value =
       let tmp = Array.create ~len:8 false in
       Bit_array.blit_int ~src:value ~dst:tmp;
       print_endline (Printf.sprintf "%s // %s" (Bit_array.to_string tmp) name)
     in
     print_endline "// Initial memory contents for Visa";
     print_endline
       (Printf.sprintf
          "// Generated by: ./get_date.exe %s"
          (Sys.get_argv ()
           |> Array.to_list
           |> List.tl_exn
           |> List.map ~f:(Printf.sprintf "'%s'")
           |> String.concat ~sep:" "));
     print "sec" ofday.sec;
     print "min" ofday.min;
     print "hou" ofday.hr;
     print "day" (date |> Core.Date.day |> Int.pred);
     print "mon" (date |> Core.Date.month |> Core.Month.to_int |> Int.pred);
     print "yea" ((date |> Core.Date.year) % 100);
     ())
;;

let () = Cmdlang_to_cmdliner.run cmd ~name:"get_date" ~version:"%%VERSION%%"
